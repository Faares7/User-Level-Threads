.section .text
    .globl thread_switch
    .align 2
thread_switch:
    # a0 = old context pointer (first arg)
    # a1 = new context pointer (second arg)
    
    # Save registers into old context
    sd sp, 0(a0)         # save stack pointer
    sd ra, 8(a0)         # save return address
    sd s0, 16(a0)        # save frame pointer / s0
    sd s1, 24(a0)
    sd s2, 32(a0)
    sd s3, 40(a0)
    sd s4, 48(a0)
    sd s5, 56(a0)
    sd s6, 64(a0)
    sd s7, 72(a0)
    sd s8, 80(a0)
    sd s9, 88(a0)
    sd s10, 96(a0)
    sd s11, 104(a0)
    
    # Load registers from new context
    ld sp, 0(a1)         # restore stack pointer
    ld ra, 8(a1)         # restore return address
    ld s0, 16(a1)        # restore frame pointer / s0
    ld s1, 24(a1)
    ld s2, 32(a1)
    ld s3, 40(a1)
    ld s4, 48(a1)
    ld s5, 56(a1)
    ld s6, 64(a1)
    ld s7, 72(a1)
    ld s8, 80(a1)
    ld s9, 88(a1)
    ld s10, 96(a1)
    ld s11, 104(a1)
    
    ret
